# vim:ft=zsh
## This is a post-update hook for pkg (a vcs-home implementation).
## It includes configs provided by other packages
## and sets some more static parameters
mkdir -p $HOME/var/cache/zsh
mkdir -p $PKG_DIR/zsh/provided
rm -f $HOME/var/cache/zsh/compdump
echo -n > $PKG_DIR/zsh/provided/includes
echo -n > $PKG_DIR/zsh/provided/env

typeset -a futurepath
for inc in $PKG_DIR/*/provides/zsh/{functions,completions}(N); {
	futurepath+=${inc/$HOME/\$HOME}
}
if [[ -n $futurepath ]] {
	echo "fpath=($futurepath \$fpath)" > $PKG_DIR/zsh/provided/env
}
for inc in $PKG_DIR/*/provides/zsh/functions(*N:t); {
	echo "autoload $inc" >> $PKG_DIR/zsh/provided/includes
}

for snippet in $PKG_DIR/*/provides/zsh/*(.N); {
	echo "source ${snippet/$HOME/\$HOME}" >> $PKG_DIR/zsh/provided/includes
}

cd $PKG_DIR/zsh/etc
zcompile functions.zwc functions/*
zcompile completions.zwc completions/*

# Preserve old history
if [[ ! -e ~/var/cache/zsh/history ]] {
	cp ~/.histfile ~/var/cache/zsh/history
}
