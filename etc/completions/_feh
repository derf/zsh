#compdef feh
## completion for feh 1.3.4.dfsg.1-1, based on feh(1)
## Daniel Friesel <derf@derf.homelinux.org>

local arguments feh_themes

feh_themes='cut -d " " -f 1 .fehrc | grep -v "^#" | grep "\w"'

arguments=(
	{-h,--help}'[Show help]'
	{-v,--version}'[Show version information]'
	{-V,--verbose}'[Be verbose]'
	{-q,--quiet}'[Suppress non-fatal errors]'
	{-T+,--theme}'[Load named options from config]:theme:($(eval $feh_themes))'
	'--rcfile[Specify config file]:file:_files'
	{-r,--recursive}'[Recurse into subdirectories]'
	{-z,--randomize}'[Randomize file list before displaying]'
	{-f+,--filelist}'[Read file list from this file]:file:_files'
	{-p,--preload}'[Eliminate unlaodable images before displaying]'
	{-F,--full-screen}'[Make the window fullscreen]'
	{-g+,--geometry}'[Limit window size]:geometry: '
	{-Z,--auto-zoom}'[Zoom picture to screen size]'
	'--zoom[Zoom images]:percent: '
	{-w,--multiwindow}'[Multiple windows, one image per window]'
	{-x,--borderless}'[Borderless windows]'
	{-d,--draw-filename}'[Show filename in images]'
	{-D+,--slideshow-delay}'[Time delay for automatic slidechange]:time: '
	{-R+,--reload}'[Time delay for reloading]:time: '
	{-k,--keep-http}"[Don't delete cached files]"
	{-Q,--builtin}'[Use builtin http client]'
	'--caption-path[Where to look for image captions]:relative directory: '
	{-l,--list}'[List images, their size, etc.]'
	{-L+,--customlist}'[Format list output]:format: '
	{-U,--loadable}'[Print loadable images]'
	{-u,--unloadable}'[Print unloadable images]'
	{-S+,--sort}'[Sort images]:sort type:(name filename width height pixels size format)'
	{-n,--reverse}'[Reverse sort order]'
	{-A,--action}'[Action to perform on each image]:shell: '
	--action{1..9}':shell: '
	{-m,--montage}'[Enable montage mode]'
	{-c,--collage}'[Enable collage mode]'
	{-i,--index}'[Enable index mode]'
	{-I,--fullindex}'[Enable verbose index mode]'
	{-t,--thumbnails}'[Enable interactive index mode]'
	'--bg-tile[Set tiled desktop background]:file:_files'
	'--bg-center[Set centered desktop background]:file:_files'
	'--bg-scale[Set scaled desktop background]:file:_files'
	'--bg-seamless[Set scaled desktop background, preserving aspect]:file:_files'
	{-M,--menu-font}'[Set font in menus]:font: '
	'--menu-style[Style descriptor for menu text]:file:_files'
	'--menu-bg[Background image in menus]:file:_files'
	'--menu-border[Set menu background border]:integer: '
	{-N,--no-menus}"[Don't load or show any menus]"
	{-1,--next-button}'[Button for next image]:button:(1 2 3)'
	{-2,--zoom-button}'[Button to zoom image]:button:(1 2 3)'
	{-3,--pan-button}'[Ctrl+Button to pan image]:button:(1 2 3)'
	'--no-pan-ctrl-mask[Pan without pressing ctrl]'
	{-4,--menu-button}'[Button to activate menu]:button:(1 2 3)'
	{-5,--no-menu-ctrl-mask}'[Show Mune without pressing ctrl]'
	{-6,--rotate-button}'[Ctrl+Button to rotate image]:button:(1 2 3)'
	{-7,--no-rotate-ctrl-mask}'[Rotate without pressing ctrl]'
	{-8,--blur-button}'[Ctrl+Button tu blur image]:button:(1 2 3)'
	{-9,--no-blur-ctrl-mask}'[Blur without pressing ctrl]'
	'--xinerama[Toggle xinerama support]'
	'--screen-clip[Toggle window clipping]'
	'--hide-pointer[Hide mouse pointer in fullscreen mode]'
	{-X,--ignore-aspect}"[montage mode: Don't preserve aspect ratio]"
	{-s,--stretch}'[montage mode: Enlarge images to fit thumbnail size]'
	{-y,--thumb-width}'[montage mode: Thumbnail width]:pixels: '
	{-E,--thumb-height}'[montage mode: Thumbnail height]:pixels: '
	{-W,--limit-width}'[montage mode: Limit montage width]:pixels: '
	{-H,--limit-height}'[montage mode: Limit montage heihgt]:pixels: '
	{-b,--bg}'[montage mode: Background image]:file:_files' #TODO also offer 'trans'
	{-a,--alpha}'[montage mode: Thumbnail transparency level]:integer: '
	'-o[montage mode: Output file]:file:_files'
	"-O[montage mode: Output file, don't display montage]:file:_files"
	{-e,--font}'[index mode: Thumbnail info font]:font: '
	'*--fontpath[index mode: directory to search for fonts]:directory:_path_files -/'
	'--title-font[index mode: title font]:font: '
	'*:file:_files'
)

_arguments -s $arguments
