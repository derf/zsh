#compdef fbi
## completion for fbi <http://linux.bytesex.org/fbida/> 2.07-1, based on fbi -h
## Copyright (C) 2008 by Birte Friesel <derf@finalrewind.org>
## License: WTFPL <http://sam.zoy.org/wtfpl>:
##   0. You just DO WHAT THE FUCK YOU WANT TO.
##
## http://finalrewind.org/git/zsh/plain/etc/completions/_fbi


typeset -a arguments

arguments=(
	-{1,once}"[don't loop]"
	-{a,autozoom}'[autozoom]'
	'-autodown[zoom out only]'
	'-autoup[zoom in only]'
	'-backup[editing: create backup files]'
	'-blend[Blend images]:miliseconds: '
	'-cachemem[cache size]:megabytes: '
	'-comments[display image comments]'
	-{d,device}'[set framebuffer device]:framebuffer device:_path_files -W /dev -g "fb*"'
	-{e,edit}'[enable editing]'
	-{f,font}'[set font]:font:_font'
	'-fitwidth[zoom to width only]'
	-{g,gamma}'[gamma correction]:gamma: '
	-{h,help}'[show help]'
	-{l,list}'[read imagelist from file]:file:_files'
	-{m,mode}'[set video mode]:video mode: '
	'-preserve[editing: preserve timestamps]'
	-{P,text}'[enable textreading mode]'
	-{r,resolution}'[select PhotoCD resolution]:resolution:(1 2 3 4 5)'
	'-readahead[pre-cache images]'
	-{s,scroll}'[set scroll steps]:pixels: '
	'-store[write commandline args to config]'
	-{t,timeout}'[slideshow timeout]:time: '
	-{T,vt}'[start on virtual console]:virtual console: '
	-{u,random}'[randomize filename order]'
	-{v,verbose}'[verbose (show status line)]'
	-{V,version}'[show version information]'
	'*:file:_files'
)

_arguments -s ${arguments}

function _font () {
	typeset IFS=$'\n'
	typeset -a fonts
	_wanted font expl 'font' \
	compadd $(fc-list | cut -d ":" -f 1 2>&1)
}
